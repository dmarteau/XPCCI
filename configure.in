dnl Disable configure caching ... it causes lots of hassles.
define([AC_CACHE_LOAD], )
define([AC_CACHE_SAVE], )

AC_INIT()

dnl ---------------------------------------------------------------------------
dnl Runtime version
dnl ---------------------------------------------------------------------------

ac_dm_ver_major=3
ac_dm_ver_minor=1
ac_dm_ver_revision=0

AC_SUBST(DM_RUNTIME_VER_MAJOR,$ac_dm_ver_major)
AC_SUBST(DM_RUNTIME_VER_MINOR,$ac_dm_ver_minor)
AC_SUBST(DM_RUNTIME_VER_REVISION,$ac_dm_ver_revision)

AC_SUBST(topsrcdir,$(pwd))

dnl ---------------------------------------------------------------------------
dnl Check for --enable-debug
dnl ---------------------------------------------------------------------------

AC_MSG_CHECKING([for enable debug build])

AC_ARG_ENABLE(debug,
	      AS_HELP_STRING([--enable-debug],[enable debugging (disabled by default)]),,)

if test "x$enable_debug" = "xyes" ; then
  AC_SUBST(debug_config,[DM_DEBUG:=1])
  AC_MSG_RESULT([yes])
else
  AC_MSG_RESULT([no])
fi

dnl ---------------------------------------------------------------------------
dnl Check for --with-config
dnl ---------------------------------------------------------------------------

AC_MSG_CHECKING([for build configuration])

AC_ARG_WITH(config,
	      AS_HELP_STRING([--with-config=ARG],[set the build configuration (default guess)]),
	      [], [
			case $(uname) in
				Darwin) with_config=darwin-clang ;;
				Linux)  with_config=linux-gcc ;; 
				mingw*) with_config=win-msvc ;; 
				*) AC_MSG_FAILURE([Unknown build configuration]) ;;
			esac])


if test "x$with_config" != "xno"; then
   AC_MSG_RESULT([$with_config])
else
   AC_MSG_FAILURE([Missing build configuration])
fi

AC_SUBST(DM_BUILD_CONFIG,$with_config)

case $with_config in
win-msvc)
  AC_SUBST(OS_ARCH,[WINNT])
  AC_SUBST(LIBSFX,[.lib])
  AC_SUBST(DLLSFX,[.dll])
  AC_SUBST(EXESFX,[.exe])   
;;
linux-*)  
  AC_SUBST(OS_ARCH,[Linux])
  AC_SUBST(DLLPREFIX,[lib])
  AC_SUBST(LIBPREFIX,[lib])
  AC_SUBST(LIBSFX,[.a])
  AC_SUBST(DLLSFX,[.so])
  AC_SUBST(OS_SUB_ARCH,[UNIX])
;;
darwin-*)
  AC_SUBST(OS_ARCH,[Darwin])
  AC_SUBST(DLLPREFIX,[lib])
  AC_SUBST(LIBPREFIX,[lib])
  AC_SUBST(LIBSFX,[.a])
  AC_SUBST(DLLSFX,[.dylib])
  AC_SUBST(OS_SUB_ARCH,[UNIX])
;;
*)  AC_MSG_FAILURE([Invalid build configuration])
;;
esac

case $with_config in 
*-clang)
    export CC=clang
    export CXX=clang++
;;
esac

dnl ---------------------------------------------------------------------------
dnl Set build dir
dnl ---------------------------------------------------------------------------

AC_ARG_VAR(DM_OBJDIR,[Build directory path])

if test "x$enable_debug" = "xyes" ; then
    : "${DM_OBJDIR=$(pwd)/build-debug-$with_config}" 
else
    : "${DM_OBJDIR=$(pwd)/build-release-$with_config}"
fi 

AC_MSG_NOTICE([Build directory is $DM_OBJDIR])

dnl ---------------------------------------------------------------------------
dnl Checks for programs.
dnl ---------------------------------------------------------------------------
AC_PROG_CC
AC_PROG_CXX

dnl ---------------------------------------------------------------------------
dnl Check for type size
dnl ---------------------------------------------------------------------------
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(void*)

AC_DEFINE_UNQUOTED([DM_BYTES_PER_INT],[$ac_cv_sizeof_int], [Define to the size of 'int'])
AC_DEFINE_UNQUOTED([DM_BYTES_PER_LONG],[$ac_cv_sizeof_long], [Define to the size of 'long'])
AC_DEFINE_UNQUOTED([DM_BYTES_PER_POINTER],[$ac_cv_sizeof_voidp], [Define to the size of 'void*'])

AC_CONFIG_FILES([config/config.opt])
AC_CONFIG_HEADERS([include/dm_ports/ac_dm_config.h])

dnl ---------------------------------------------------------------------------
dnl Check for --enable-tests
dnl ---------------------------------------------------------------------------

AC_MSG_CHECKING([for enable tests])

AC_ARG_ENABLE(tests,
	      AS_HELP_STRING([--enable-tests],[enable building tests (enabled by default)]),
		  [],[enable_tests=yes])

if test "x$enable_tests" = "xyes" ; then
  AC_SUBST(ENABLE_TESTS,[1])
  AC_MSG_RESULT([yes])
else
  AC_SUBST(ENABLE_TESTS,[0])
  AC_MSG_RESULT([no])
fi


dnl ---------------------------------------------------------------------------
dnl Check for python version
dnl ---------------------------------------------------------------------------

AC_PATH_PROG(ac_python3_path,[python3])
if test "x$ac_python3_path" = "x"; then
  AC_MSG_FAILURE([python3 not found])
fi

AC_CACHE_CHECK([python version], ac_cv_ver_python, [ac_cv_ver_python=`python3 -c 'import sys;print(sys.version[[:3]])'`])
if test "x$ac_cv_ver_python" \< "x3.4"; then
  AC_MSG_FAILURE([python version 3.5 minimum required])
fi

dnl ---------------------------------------------------------------------------
dnl Check for python bindings
dnl ---------------------------------------------------------------------------

AC_MSG_CHECKING([if python bindings enabled])

AC_ARG_ENABLE(cci-python,
  AS_HELP_STRING([--enable-cci-python],[enable building python bindings (disabled)]),
  [],[])

if test "x$enable_cci_python" = "xyes" ; then
  PYTHON_LIBS=`python3-config --libs`
  PYTHON_CONFIGDIR=`python3-config --configdir`
  PYTHON_INCLUDES=`python3-config --includes`
  AC_SUBST(ac_enable_python_bindings,[ENABLE_PYTHON_BINDINGS:=1])
  AC_SUBST(ac_python_ldflags,[PYTHON_LDFLAGS:="-L$PYTHON_CONFIGDIR $PYTHON_LIBS"])
  AC_SUBST(ac_python_includes,[PYTHON_INCLUDES:=$PYTHON_INCLUDES])
  AC_MSG_RESULT([yes])
else
  AC_MSG_RESULT([no])
fi


dnl ---------------------------------------------------------------------------
dnl Setup python virtual env
dnl ---------------------------------------------------------------------------

PYTHON_VENV=$DM_OBJDIR/.virtualenv

AC_MSG_NOTICE([Creating python build environment])
python3 -m venv $PYTHON_VENV

AC_SUBST(ac_python_exec,[$PYTHON_VENV/bin/python])
AC_SUBST(ac_python_pip, [$PYTHON_VENV/bin/pip])

ac_pip_install="$ac_python_pip install -q --disable-pip-version-check"

dnl install idl parser

$ac_pip_install -e $topsrcdir/tools/idl-parser/.

AC_OUTPUT()


